(load-file "../mal6/env.mal")
(load-file "../mal6/core.mal")

;; Implement some core functions in mal itself so that we can
;; self-host earlier
(def! first (fn* (lst)
  (if (> (count lst) 0)
    (let* (f (fn* (x & xs) x))
      (apply f lst))
    nil)))

(def! rest (fn* (lst)
  (if (> (count lst) 0)
    (let* (r (fn* (x & xs) xs))
      (apply r lst))
    (list))))

(def! nth (fn* (lst idx)
  (if (> idx 0)
    (nth (rest lst) (- idx 1))
    (first lst))))

(def! cons (fn* (a lst)
  (apply list a lst)))

(def! map (fn* (f lst)
  (if (> (count lst) 0)
    (cons (f (first lst))
          (map f (rest lst)))
    (list))))

;; read
(def! READ (fn* (strng)
  (read-string strng)))

;; eval
(def! eval-ast (fn* (ast env)
  (if (symbol? ast)
    (env-get env ast)

    (if (list? ast)
        (map (fn* (exp) (EVAL exp env)) ast)

        ast))))

(def! LET (fn* (env args)
  (if (> (count args) 0)
    (do
      (env-set env (nth args 0) (EVAL (nth args 1) env))
      (LET env (rest (rest args)))))))

(def! EVAL (fn* (ast env)
  (if (not (list? ast))
    (eval-ast ast env)

    ;; apply list
    (let* (a0 (first ast))
      (if (= nil a0)
        ast

      (if (= "def!" (str a0))
        (env-set env (nth ast 1) (EVAL (nth ast 2) env))

      (if (= "let*" (str a0))
        (let* (let-env (new-env env))
          (do
            (LET let-env (nth ast 1))
            (EVAL (nth ast 2) let-env)))

      (if (= "do" (str a0))
        (let* (el (eval-ast (rest ast) env))
          (nth el (- (count el) 1)))

      (if (= "if" (str a0))
        (let* (cond (EVAL (nth ast 1) env))
          (if (not cond)
            (if (> (count ast) 3)
              (EVAL (nth ast 3) env)
              nil)
            (EVAL (nth ast 2) env)))

      (if (= "fn*" (str a0))
        (fn* (& args)
          (EVAL (nth ast 2) (new-env env (nth ast 1) args)))

      (let* (el (eval-ast ast env)
            f (first el)
            args (rest el))
        (apply f args))))))))))))

;; print
(def! PRINT (fn* (exp) (pr-str exp)))

;; repl
(def! repl-env (new-env))
(def! rep (fn* (strng)
  (PRINT (EVAL (READ strng) repl-env))))

;; core.mal: defined directly using host mal
(map (fn* (data) (env-set repl-env (nth data 0) (nth data 1))) core_ns)
(env-set repl-env (read-string "eval") (fn* (ast) (EVAL ast repl-env)))
(env-set repl-env (read-string "*ARGV*") (rest *ARGV*))

;; core.mal: defined using the new language itself
(rep (str "(def! *host-language* \"" *host-language* "-mal\")"))
(rep "(def! not (fn* (a) (if a false true)))")
(rep "(def! load-file (fn* (f) (eval (read-string (str \"(do \" (slurp f) \")\")))))")

;; repl loop
(def! repl-loop (fn* ()
  (let* (line (readline "mal-user> "))
    (if line
      (do
        (if (not (= "" line))
          (println (rep line)))
        (repl-loop))))))

;; Script mode or interactive REPL
(def! -main (fn* (& args) 
  (if (> (count args) 0)
    (rep (str "(load-file \"" (first args) "\")"))
    (do
      (rep "(println (str \"Mal [\" *host-language* \"]\"))")
      (repl-loop)))))
(apply -main *ARGV*)
